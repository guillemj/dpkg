LIBDPKG_0 {
global:
	# Error reporting
	dpkg_error_destroy;

	# Charset and string functions
	dpkg_locales_init;

	# Program name
	dpkg_set_progname;
	dpkg_get_progname;

	# Program setup/teardown
	dpkg_program_init;
	dpkg_program_done;

	# Ar support
	dpkg_ar_normalize_name;
	dpkg_ar_member_is_illegal;

local:
	*;
};

LIBDPKG_PRIVATE {
	# Error handling
	push_error_context_jump;
	push_error_context_func;
	push_error_context;
	pop_error_context;
	print_fatal_error;
	catch_fatal_error;
	push_checkpoint;
	push_cleanup;
	pop_cleanup;
	onerr_abort;		# XXX variable, do not export
	ohshitv;
	ohshite;
	ohshit;
	do_internerr;
	dpkg_set_report_piped_mode;
	dpkg_set_report_buffer;
	warning_get_count;
	warningv;
	warning;
	notice;

	debug_set_output;
	debug_set_mask;
	debug_has_flag;
	debug;

	# Generic cleanup
	cu_closepipe;
	cu_closestream;
	cu_closedir;
	cu_closefd;
	cu_filename;

	# ‘Must do’ functions
	m_malloc;
	m_calloc;
	m_realloc;
	m_strdup;
	m_asprintf;
	m_dup2;
	m_pipe;
	m_output;
	fgets_must;
	fgets_checked;

	# Charset and string functions
	cisdigit;

	str_match_end;
	str_escape_fmt;
	str_strip_quotes;
	str_quote_meta;
	str_width;
	str_gen_crop;

	# Variable buffer support
	varbuf_init;
	varbuf_reset;
	varbuf_grow;
	varbuf_trunc;
	varbuf_add_char;
	varbuf_dup_char;
	varbuf_map_char;
	varbuf_add_buf;
	varbuf_end_str;
	varbuf_printf;
	varbuf_vprintf;
	varbuf_detach;
	varbuf_destroy;

	# Path, directory and file functions
	path_skip_slash_dotslash;
	path_trim_slash_slashdot;
	path_basename;
	path_make_temp_template;
	path_quote_filename;

	dir_sync_path;
	dir_sync_path_parent;
	dir_sync_contents;

	file_copy_perms;
	file_show;

	atomic_file_new;
	atomic_file_open;
	atomic_file_sync;
	atomic_file_close;
	atomic_file_commit;
	atomic_file_remove;
	atomic_file_free;

	glob_list_prepend;
	glob_list_free;

	# Data structure functions
	namevalue_find_by_name;

	# Buffer I/O functions
	fd_read;
	fd_write;
	buffer_filter;
	buffer_skip_*;
	buffer_copy_*;

	# Subprocess and command handling
	subproc_signals_setup;
	subproc_signals_cleanup;
	subproc_fork;
	subproc_wait;
	subproc_check;
	subproc_wait_check;

	command_init;
	command_add_arg;
	command_add_argl;
	command_add_argv;
	command_add_args;
	command_exec;
	command_get_pager;
	command_shell;
	command_destroy;

	setcloexec;

	# Compression support
	compressor_find_by_name;
	compressor_find_by_extension;
	compressor_get_name;
	compressor_get_extension;
	compressor_get_strategy;
	compressor_check_params;
	compress_filter;
	decompress_filter;

	# Ar support
	dpkg_ar_put_magic;
	dpkg_ar_member_put_header;
	dpkg_ar_member_put_file;
	dpkg_ar_member_put_mem;
	dpkg_ar_member_get_size;

	# deb version support
	deb_version_parse;

	# Configuration and command line handling
	dpkg_options_load;
	dpkg_options_parse;
	dpkg_options_parse_arg_int;
	badusage;
	cipaction;		# XXX variable, do not export
	setaction;
	setobsolete;

	# General logging
	log_file;		# XXX variable, do not export
	log_message;

	# Action logging
	statusfd_add;
	statusfd_send;

	# Progress report support
	progress_init;
	progress_step;
	progress_done;

	# Tar support
	tar_extractor;

	# Non-freeing malloc (pool/arena)
	nfmalloc;
	nfstrnsave;
	nfstrsave;
	nffreeall;

	# Version struct handling
	dpkg_version_blank;
	dpkg_version_is_informative;
	dpkg_version_compare;
	dpkg_version_relate;
	versiondescribe;
	parseversion;

	# Architecture database
	dpkg_arch_name_is_illegal;
	dpkg_arch_describe;
	dpkg_arch_find;
	dpkg_arch_get;
	dpkg_arch_get_list;
	dpkg_arch_reset_list;
	dpkg_arch_add;
	dpkg_arch_unmark;
	dpkg_arch_load_list;
	dpkg_arch_save_list;

	# Package struct handling
	pkgset_blank;
	pkgset_link_pkg;
	pkgset_installed_instances;
	pkg_blank;
	pkgbin_blank;
	pkg_name_is_illegal;
	pkg_set_status;
	pkg_set_eflags;
	pkg_clear_eflags;
	pkg_reset_eflags;
	pkg_copy_eflags;
	pkg_set_want;
	pkg_is_informative;
	copy_dependency_links;
	pkg_sorter_by_nonambig_name_arch;
	varbuf_add_pkgbin_name;
	varbuf_add_archqual;
	pkgbin_name;
	pkg_name;
	pkg_summary;
	pkg_abbrev_want;
	pkg_abbrev_status;
	pkg_abbrev_eflag;
	pkg_want_name;
	pkg_eflag_name;
	pkg_status_name;
	pkg_priority_name;

	# Package list handling
	pkg_list_new;
	pkg_list_free;
	pkg_list_prepend;

	# Package array handling
	pkg_array_init_from_db;
	pkg_array_sort;
	pkg_array_destroy;

	# Package queue handling
	pkg_queue_init;
	pkg_queue_destroy;
	pkg_queue_is_empty;
	pkg_queue_push;
	pkg_queue_pop;

	# Package field format handling
	pkg_format_parse;
	pkg_format_show;
	pkg_format_free;

	# Package specifiers
	pkg_spec_is_illegal;
	pkg_spec_init;
	pkg_spec_destroy;
	pkg_spec_set;
	pkg_spec_parse;
	pkg_spec_match_pkg;
	pkg_spec_parse_pkg;
	pkg_spec_find_pkg;
	pkg_spec_iter_init;
	pkg_spec_iter_next_pkg;
	pkg_spec_iter_destroy;

	# Package in-core database functions
	pkg_db_find_set;
	pkg_db_find_singleton;
	pkg_db_find_pkg;
	pkg_db_count_set;
	pkg_db_count_pkg;
	pkg_db_reset;
	pkg_db_iter_new;
	pkg_db_iter_next_set;
	pkg_db_iter_next_pkg;
	pkg_db_iter_free;

	# Dependency and Conflict functions
	deparchsatisfied;
	archsatisfied;
	versionsatisfied;

	# Package on-disk database functions
	varbufdependency;
	varbufrecord;
	writerecord;
	parsedb;
	writedb;

	dpkg_db_set_dir;
	dpkg_db_get_dir;
	dpkg_db_get_path;

	# Log based package on-disk database support
	modstatdb_init;
	modstatdb_is_locked;
	modstatdb_can_lock;
	modstatdb_lock;
	modstatdb_unlock;
	modstatdb_open;
	modstatdb_get_status;
	modstatdb_note;
	modstatdb_note_ifwrite;
	modstatdb_checkpoint;
	modstatdb_shutdown;
	modstatdb_done;

	# Triggers support
	trig_name_is_illegal;
	trigdef_set_methods;
	trigdef_update_start;
	trigdef_update_printf;
	trigdef_parse;
	trigdef_process_done;
	trig_override_hooks;
	trig_file_activate_byname;
	trig_file_activate;
	trig_path_activate;
	trig_note_pend;
	trig_note_aw;
	trig_clear_awaiters;
	trig_file_interests_ensure;
	trig_file_interests_save;
	trig_cicb_interest_delete;
	trig_cicb_interest_add;
	trig_parse_ci;
	trig_cicb_statuschange_activate;
	trig_incorporate;
} LIBDPKG_0;
